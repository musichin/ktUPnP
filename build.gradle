// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

allprojects {
    ext {
        kotlin_version = '1.0.2'
    }

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: 'maven-publish'
    apply plugin: "com.jfrog.bintray"

    group = 'com.github.musichin.ktupnp'
    version = '0.0.2'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    artifacts {
        archives sourceJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['mavenJava']
        pkg {
            repo = 'maven'
            name = 'ktUPnP'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/musichin/ktUPnP.git'
        }
    }
}